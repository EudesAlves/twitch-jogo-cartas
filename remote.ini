[script]
n0=; Bot para Mini game de Cartas
n1=; versão 1.0
n2=; linguagem: português
n3=; Jan/2017
n4=
n5=on *:CONNECT: {
n6=  CAP REQ :twitch.tv/membership
n7=  CAP REQ :twitch.tv/commands
n8=
n9=
n10=}
n11=
n12=; ***** JOIN *****;
n13=on *:JOIN:#: {
n14=  ; Executado quando Bot entra no chat
n15=  if ($nick == $me) {
n16=    ; *** Variáveis Globais de Sistema ***
n17=    set %chipsFile $+(BOT/,CHIPS.ini)
n18=    set %viewersfile $+(BOT/,VIEWERS.txt)
n19=    set %modsfile  $+(BOT/,MODS.txt)
n20=    %channelowner = #
n21=    set %channelowner $right(%channelowner, -1)
n22=    set %botnick $me
n23=
n24=    write -c %viewersfile
n25=    if ($isMod(%channelowner)) { 
n26=    }
n27=    else {
n28=      /write %modsfile %channelowner
n29=    }
n30=
n31=    ; *** Variáveis Globais do Jogo ***
n32=    set %cardsIdFile $+(BOT/,CARDSID.ini)
n33=    set %cardsFile $+(BOT/,CARDS.txt)
n34=    set %cardsTempFile $+(BOT/,CARDSTEMP.txt)
n35=    set %variablesFile $+(BOT/,VARIABLES.ini)
n36=
n37=    set %gameStatus 1
n38=    set %player1Card1 0
n39=    set %player1Card2 0
n40=    set %player1Card3 0
n41=    set %player2Card1 0
n42=    set %player2Card2 0
n43=    set %player2Card3 0
n44=    set %player1CardSel 0
n45=    set %player2CardSel 0
n46=
n47=    createCardsTemp
n48=
n49=  }
n50=  ; Executado quando Viewer entra no chat
n51=  ; Cria alarme para somar Fichas
n52=  else {
n53=    $+(.timerchips.,$chan,.,$nick) 0 180 add.chips $+($chan,.,$nick) 1
n54=    /write %viewersfile $nick
n55=  }
n56=
n57=
n58=}
n59=
n60=; ***** PART *****;
n61=on !*:PART:#: {
n62=  ; Executado quando Bot ou Viewer deixa o chat
n63=  $+(.timerchips.,$chan,.,$nick) off
n64=  /write -dl $findLineByName($2, %viewersfile) %viewersfile
n65=
n66=}
n67=
n68=
n69=; Envia mensagem privada com quantidade de fichas
n70=on *:TEXT:!fichas:#: {
n71=  var %chips = $readini(%chipsFile,$+($chan,.,$nick),Chips)
n72=  msg $chan /w $nick você possui %chips fichas.
n73=}
n74=
n75=
n76=; Owner do Canal Encerra o jogo
n77=on *:TEXT:!end:#: {
n78=  if ($nick == %channelowner) {
n79=    gameEnd
n80=  }
n81=}
n82=
n83=
n84=
n85=; **** Iniciar Jogo ****
n86=on *:TEXT:!cards:#: {
n87=  if (%gameStatus == 1) {
n88=    ; Jogador 1
n89=    if ($readini(%variablesFile, Variables, player1) == 0) {
n90=      writeini -n %variablesFile Variables player1 $nick
n91=      writeini -n %variablesFile Variables player1Status 1
n92=      getCardsPlayer1
n93=      msg $chan $nick Entrou na partida.
n94=      .timerNoPlayer2 1 30 /msg $chan Sem Jogador 2. Partida encerrada.
n95=      .timerNoPlayer2End 1 31 /gameEnd
n96=    }
n97=    
n98=    ; Jogador 2
n99=    elseif ($readini(%variablesFile, Variables, player2) == 0) {
n100=      .timerNoPlayer2 off
n101=      .timerNoPlayer2End off
n102=      writeini -n %variablesFile Variables player2 $nick
n103=      writeini -n %variablesFile Variables player2Status 1
n104=      set %gameStatus 2
n105=      getCardsPlayer2
n106=      msg $chan $readini(%variablesFile, Variables, player1 ) e $nick reservaram 3 cartas cada. Digitem !1 ou !2 ou !3 para escolherem uma.
n107=      .timerNoOneGotCards 1 30 /msg $chan Ninguém escolheu. Partida encerrada.
n108=      .timerNoOneGotCardsEnd 1 31 /gameEnd
n109=
n110=    }
n111=  }
n112=}
n113=
n114=
n115=; **** Escolha de Cartas 1 , 2, 3 ****
n116=on *:TEXT:!1:#: {
n117=  if ($nick == $readini(%variablesFile, Variables, player1)) {
n118=    chooseCard %player1Card1 $nick
n119=  }
n120=  elseif ($nick == $readini(%variablesFile, Variables, player2)) {
n121=    chooseCard %player2Card1 $nick
n122=  }
n123=}
n124=; ****   ****
n125=on *:TEXT:!2:#: {
n126=  if ($nick == $readini(%variablesFile, Variables, player1)) {
n127=    chooseCard %player1Card2 $nick
n128=  }
n129=  elseif ($nick == $readini(%variablesFile, Variables, player2)) {
n130=    chooseCard %player2Card2 $nick
n131=  }
n132=}
n133=; ****   ****
n134=on *:TEXT:!3:#: {
n135=  if ($nick == $readini(%variablesFile, Variables, player1)) {
n136=    chooseCard %player1Card3 $nick
n137=  }
n138=  elseif ($nick == $readini(%variablesFile, Variables, player2)) {
n139=    chooseCard %player2Card3 $nick
n140=  }
n141=}
n142=
n143=
n144=; **** Go para chamar resultado do jogo ****
n145=on *:TEXT:!go:#: {
n146=  ; Jogador 1
n147=  if ($nick == $readini(%variablesFile, Variables, player1)) {
n148=    if ($readini(%variablesFile, Variables, player1Status) == 2) {
n149=      writeini -n %variablesFile Variables player1Status 3
n150=      .timerP2Status2 off
n151=      .timerP2Chips2 off
n152=      .timerP2Status2End off
n153=      .timerP1Status1 off
n154=      .timerP1Status1End off
n155=      
n156=      if ($readini(%variablesFile, Variables, player2Status) == 3) {
n157=        set %gameStatus 3
n158=        gameResult
n159=      }
n160=      else {
n161=            .timerP1Status2 1 30 /msg $chan Partida encerrada. $nick recebeu 30 fichas.
n162=            .timerP1Chips2 1 31 /add.chips $+(#,.,$readini(%variablesFile, Variables, player1)) 30
n163=            .timerP1Status2End 1 32 /gameEnd
n164=          }
n165=    }
n166=  }
n167=  ; Jogador 2  
n168=  elseif ($nick == $readini(%variablesFile, Variables, player2)) {
n169=    if ($readini(%variablesFile, Variables, player2Status) == 2) {
n170=      writeini -n %variablesFile Variables player2Status 3
n171=      .timerP1Status2 off
n172=      .timerP1Chips2 off
n173=      .timerP1Status2End off
n174=      .timerP2Status1 off
n175=      .timerP2Status1End off
n176=      
n177=      if ($readini(%variablesFile, Variables, player1Status) == 3) {
n178=        set %gameStatus 3
n179=        gameResult
n180=      }
n181=      else {
n182=            .timerP2Status2 1 30 /msg $chan Partida encerrada. $nick recebeu 30 fichas.
n183=            .timerP2Chips2 1 31 /add.chips $+(#,.,$readini(%variablesFile, Variables, player1)) 30
n184=            .timerP2Status2End 1 32 /gameEnd
n185=          }
n186=    }
n187=  }
n188=}
n189=
n190=on *:TEXT:*:#: {
n191=  ; Owner pode Adicionar e Remover usuários com permissão de adicionar fichas
n192=  ; ** ADICIONAR 'MOD' **
n193=  ;$1 = !addmod
n194=  ;$2 = Nome do usuário
n195=  if ($1 == !addmod) {
n196=    if ($0 == 2) {
n197=      if ($nick == %channelowner) {
n198=        if ($isMod($2)) { }
n199=        else {
n200=          /write %modsfile $2
n201=        }
n202=      }
n203=    }
n204=  }
n205=  ; ** REMOVER 'MOD' **
n206=  ;$1 = !remmod
n207=  ;$2 = Nome do usuário
n208=  elseif ($1 == !remmod) {
n209=    if ($0 == 2) {
n210=      if ($nick == %channelowner) {
n211=        if ($isMod($2)) { 
n212=          /write -dl $findLineByName($2, %modsfile) %modsfile
n213=        }
n214=      }
n215=    }
n216=  }
n217=
n218=  ; ** ADICIONAR FICHAS **
n219=  ;$1 = !addchips
n220=  ;$2 = Nome do usuário
n221=  ;$3 = Quantidade
n222=  elseif ($1 = !addchips) {
n223=    if ($0 < 3) {
n224=      msg $chan Digite: !addchips nome quantidade
n225=    }
n226=    else {
n227=      add.chips $+($chan,.,$2) $3 
n228=    }
n229=  }
n230=  ; ** REMOVER FICHAS **
n231=  ;$1 = !removechips
n232=  ;$2 = Nome do usuário
n233=  ;$3 = Quantidade
n234=  elseif ($1 = !removechips) {
n235=    if ($0 < 3) {
n236=      msg $chan Digite: !removechips nome quantidade
n237=    }
n238=    else {
n239=      rem.chips $+($chan,.,$2) $3 
n240=    }
n241=  }
n242=}
n243=
n244=
n245=
n246=
n247=; ************************************************  ALIASES  ************************************************
n248=
n249=
n250=; **** Alias Identifier Retorna Linha por Nome **** ;
n251=alias findLineByName {
n252=  ;$1 = NOME
n253=  ;$2 = ARQUIVO TXT
n254=  var %i = 1
n255=  //while ($read($2, n, %i)) {
n256=    if ($1 == $read($2, n, %i)) {
n257=      return %i
n258=    }
n259=    inc %i
n260=  }
n261=  return 0
n262=}
n263=
n264=
n265=; **** Alias Identifier Retorna Se Encontrou Nome **** ;
n266=alias findNameTxt {
n267=  ;$1 = NOME
n268=  ;$2 = ARQUIVO TXT
n269=  var %i = 1
n270=  //while ($read($2, n, %i)) {
n271=    if ($1 == $read($2, n, %i)) {
n272=      return 1
n273=    }
n274=    inc %i
n275=  }
n276=  return 0
n277=}
n278=
n279=; **** Alias Identifier Retorna se É Mod por Nome **** ;
n280=alias isMod {
n281=  ;$1 = NOME
n282=  return $findNameTxt($1, %modsfile)
n283=
n284=}
n285=
n286=
n287=; **** Alias Adicionar Fichas **** ;
n288=alias -l add.chips {
n289=  writeini -n %chipsFile $1 Chips $calc($readini(%chipsFile,$1,Chips) + $2)
n290=}
n291=
n292=; **** Alias Remover Fichas **** ;
n293=alias -l rem.chips {
n294=  writeini -n %chipsFile $1 Chips $calc($readini(%chipsFile,$1,Chips) - $2)
n295=  var %chips = $readini(%chipsFile,$1,Chips)
n296=  if (%chips < 0) {
n297=    writeini -n %chipsFile $1 Chips 0
n298=  }
n299=}
n300=
n301=
n302=; **** Alias Criar CARDSID ini **** ;
n303=; cria arquivo com IDs dos Cards usando o arquivo de Cards.
n304=; não é utilizado durante o jogo
n305=alias createCardsidIni {
n306=  var %i = 1
n307=  var %id = 52
n308=  var %card = 0
n309=  //while ($read(%cardsFile, n, %i)) {
n310=    set %card $read(%cardsFile, n, %i)
n311=    writeini -n %cardsIdFile Cards %card %id
n312=
n313=    inc %i
n314=    dec %id
n315=  }
n316=}
n317=
n318=; **** Alias Criar CARDSTEMP **** ;
n319=; preenche arquivo de Cards temporários usados durante o jogo
n320=alias createCardsTemp {
n321=  var %i = 1
n322=  /write -c %cardsTempFile
n323=
n324=  //while ($read(%cardsFile, n, %i)) {
n325=    set %card $read(%cardsFile, n, %i)
n326=    /write %cardsTempFile %card
n327=    inc %i
n328=  }
n329=}
n330=
n331=
n332=; **** Alias Pegar Cartas para Jogador 1 ****
n333=alias getCardsPlayer1 {
n334=  set %player1Card1 $read(%cardsTempFile)
n335=  /write -dl $findLineByName(%player1Card1, %cardsTempFile) %cardsTempFile
n336=
n337=  set %player1Card2 $read(%cardsTempFile)
n338=  /write -dl $findLineByName(%player1Card2, %cardsTempFile) %cardsTempFile 
n339=
n340=  set %player1Card3 $read(%cardsTempFile)
n341=  /write -dl $findLineByName(%player1Card3, %cardsTempFile) %cardsTempFile
n342=}
n343=
n344=; **** Alias Pegar Cartas para Jogador 2 ****
n345=alias getCardsPlayer2 {
n346=  set %player2Card1 $read(%cardsTempFile)
n347=  /write -dl $findLineByName(%player2Card1, %cardsTempFile) %cardsTempFile
n348=
n349=  set %player2Card2 $read(%cardsTempFile)
n350=  /write -dl $findLineByName(%player2Card2, %cardsTempFile) %cardsTempFile 
n351=
n352=  set %player2Card3 $read(%cardsTempFile)
n353=  /write -dl $findLineByName(%player2Card3, %cardsTempFile) %cardsTempFile
n354=}
n355=
n356=
n357=; **** Alias Jogador escolhe Carta com !1 !2 !3 ****
n358=alias chooseCard {
n359=  ;$1 - variáveis playerCard 
n360=  ;$2 - $nick
n361=  if (%gameStatus == 2) {
n362=    ; Jogador 1
n363=    if ($2 == $readini(%variablesFile, Variables, player1)) {
n364=      if ($readini(%variablesFile, Variables, player1Status) == 1) {
n365=        writeini -n %variablesFile Variables player1Status 2
n366=        .timerNoOneGotCards off
n367=        .timerNoOneGotCardsEnd off
n368=        set %player1CardSel $1
n369=        msg $chan $2 você recebeu uma carta. Você pode trocar de carta ou Digitar !go para seguir.
n370=        msg $chan /w $2 Você possui um %player1CardSel
n371=        
n372=        .timerP1Status1 1 33 /msg $chan Partida encerrada.
n373=        .timerP1Status1End 1 34 /gameEnd
n374=      }
n375=      elseif ($readini(%variablesFile, Variables, player1Status) == 2) {
n376=        if (%player1CardSel != $1) {
n377=          writeini -n %variablesFile Variables player1Status 3
n378=          .timerP2Status2 off
n379=          .timerP2Chips2 off
n380=          .timerP2Status2End off
n381=          .timerP1Status1 off
n382=          .timerP1Status1End off
n383=          set %player1CardSel $1
n384=          msg $chan $2 trocou sua carta.
n385=          msg $chan /w $2 Você possui um %player1CardSel
n386=          
n387=          if ($readini(%variablesFile, Variables, player2Status) == 3) {
n388=            set %gameStatus 3
n389=            gameResult
n390=          }
n391=          else {
n392=            .timerP1Status2 1 30 /msg $chan Partida encerrada. $2 recebeu 30 fichas.
n393=            .timerP1Chips2 1 31 /add.chips $+(#,.,$readini(%variablesFile, Variables, player1)) 30
n394=            .timerP1Status2End 1 32 /gameEnd
n395=          }
n396=        }
n397=      }
n398=    }
n399=    
n400=    ; Jogador 2
n401=    elseif ($2 == $readini(%variablesFile, Variables, player2)) {
n402=      if ($readini(%variablesFile, Variables, player2Status) == 1) {
n403=        writeini -n %variablesFile Variables player2Status 2
n404=        .timerNoOneGotCards off
n405=        .timerNoOneGotCardsEnd off
n406=        set %player2CardSel $1
n407=        msg $chan $2 você recebeu uma carta. Você pode trocar de carta ou Digitar !go para seguir.
n408=        msg $chan /w $2 Você possui um %player2CardSel
n409=        
n410=        .timerP2Status1 1 30 /msg $chan Partida encerrada.
n411=        .timerP2Status1End 1 31 /gameEnd
n412=  
n413=      }
n414=      elseif ($readini(%variablesFile, Variables, player2Status) == 2) {
n415=        if (%player2CardSel != $1) {
n416=          writeini -n %variablesFile Variables player2Status 3
n417=          .timerP1Status2 off
n418=          .timerP1Chips2 off
n419=          .timerP1Status2End off
n420=          .timerP2Status1 off
n421=          .timerP2Status1End off
n422=          set %player2CardSel $1
n423=          msg $chan $2 trocou sua carta.
n424=          msg $chan /w $nick Você possui um %player2CardSel
n425=
n426=          if ($readini(%variablesFile, Variables, player1Status) == 3) {
n427=            set %gameStatus 3
n428=            gameResult
n429=          }
n430=          else {
n431=            .timerP2Status2 1 30 /msg $chan Partida encerrada. $2 recebeu 30 fichas.
n432=            .timerP2Chips2 1 31 /add.chips $+(#,.,$readini(%variablesFile, Variables, player2)) 30
n433=            .timerP2Status2End 1 32 /gameEnd
n434=          }
n435=        }
n436=      }
n437=    }
n438=  }
n439=}
n440=
n441=
n442=
n443=; **** Alias Mostrar Resultado do Jogo ****
n444=alias gameResult {
n445=  var %player1CardId = 0
n446=  var %player2CardId = 0
n447=  set %player1CardId $readini(%cardsIdFile, Cards, %player1CardSel)
n448=  set %player2CardId $readini(%cardsIdFile, Cards, %player2CardSel)
n449=  
n450=  if (%player1CardId > %player2CardId) {
n451=    .timerResultMsg1 1 10 msg $chan Mostrar Cartas. $readini(%variablesFile, Variables, player1) tem um %player1CardSel  $readini(%variablesFile, Variables, player2) tem um %player2CardSel
n452=    .timerResultMsg2 1 20 /msg $chan $readini(%variablesFile, Variables, player1) venceu essa rodada e recebeu 30 fichas.
n453=    add.chips $+( # ,.,$readini(%variablesFile, Variables, player1)) 30
n454=  }
n455=  else {
n456=    .timerResultMsg1 1 10 msg $chan Mostrar Cartas. $readini(%variablesFile, Variables, player1) tem um %player1CardSel  $readini(%variablesFile, Variables, player2) tem um %player2CardSel
n457=    .timerResultMsg2 1 20 /msg $chan $readini(%variablesFile, Variables, player2) venceu essa rodada e recebeu 30 fichas.
n458=    add.chips $+( # ,.,$readini(%variablesFile, Variables, player2)) 30
n459=  }
n460=  ; Delay para Reabilitar partida
n461=  .timerRestartMsg1 1 23 msg $chan A Partida será habilitada em 10 segundos.
n462=  gameEnd
n463=
n464=}
n465=
n466=; **** Alias para Terminar jogo  ****
n467=; Reinicia o estado do jogo
n468=alias gameEnd {
n469=  set %gameStatus 0
n470=  writeini -n %variablesFile Variables player1 0
n471=  writeini -n %variablesFile Variables player2 0
n472=  writeini -n %variablesFile Variables player1Status 0
n473=  writeini -n %variablesFile Variables player2Status 0
n474=
n475=  set %player1Card1 0
n476=  set %player1Card2 0
n477=  set %player1Card3 0
n478=  set %player2Card1 0
n479=  set %player2Card2 0
n480=  set %player2Card3 0
n481=  createCardsTemp
n482=  
n483=  .timerRestartStatus 1 35 set %gameStatus 1
n484=}
n485=;     F i m  
